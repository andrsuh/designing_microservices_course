plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.8'
    id "com.jfrog.artifactory" version "4.9.8"
    id "io.spring.dependency-management"
    id 'maven-publish'
}

group = 'com.microservices.ordermanagement'
version = apiVersion

jar {
    archiveBaseName = "ordermanagement-api"
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.8.0'
    compile 'javax.validation:validation-api:2.0.0.Final'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    compile 'org.springframework.cloud:spring-cloud-openfeign-core'
    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
}

sourceSets {
    main {
        proto {
            srcDir "$projectDir/src/main/java/com/microservices/ordermanagement/api/schema"
            include '**/*.proto'
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${releaseTrainVersion}"
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "${project.name}"
            groupId = group
            version = project.version
            from components.java
        }
    }
}

artifactory {
    contextUrl = "${artifactoryUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey  = "${artifactoryRepo}"
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
            maven = true
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey  = "${artifactoryRepo}"
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
            maven = true
        }
    }
}